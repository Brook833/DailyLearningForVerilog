# Verilog 条件语句
## 关键词：if,选择器
## 条件语句
条件(if)语句用于控制执行语句要根据条件判断来确定是否执行。
条件语句用关键字if和else来声明，条件表达式必须在圆括号中。
条件语句使用结构说明如下:

```verilog
if (condition1)       true_statement1 ;
else if (condition2)        true_statement2 ;
else if (condition3)        true_statement3 ;
else                      default_statement ;
```

> + 语句执行时，如果condition1为真，则执行true_statement1;如果 condition1 为假，condition2 为真，则执行 true_statement2；依次类推。
> + else if与 else结构可以省略，即可以只有一个if条件判断和一组执行语句true_stataement1就可以构成一个执行过程。
> + else if 可以叠加多个，不仅限于1个或2个。
> + true_statement1等执行语句可以是一条语句，也可以是多条。如果是多条执行语句，则需要用begin与end关键字进行说明。

下面代码实现了一个4路选择器的功能：

```verilog
module mux4to1(
    input [1:0]     sel ,
    input [1:0]     p0 ,
    input [1:0]     p1 ,
    input [1:0]     p2 ,
    input [1:0]     p3 ,
    output [1:0]    sout);

    reg [1:0]       sout_t;

    always @(*) begin
        if (sel == 2'b00)
            sout_t = p0 ;
        else if (sel == 2'b01)
            sout_t = p1 ;
        else if (sel == 2'b10)
            sout_t = p2 ;
        else
            sout_t = p3 ;
    end
    assign sout = sout_t ;
endmodule
```

如果是if-if-else的形式，即便执行语句只有一条，不使用begin与end关键字也会引起歧义。

例如下面代码，虽然格式上加以区分，但是 else 对应哪一个 if 条件，是有歧义的。

```verilog
if (en)
    if (sel == 2'b1)
        sout = p1s;
    else
        sout = p0;
```
     
当然，编译器一般按照就近原则，使else与最近的一个if(例子中第二个if)相对应。
但显然这样的写法时不规范且不安全的。
所以条件语句中加入begin与and关键字就是一个很好的习惯。
例如上述代码稍作修改，就不会再有书写上的歧义。

```verilog
if(en) begin
    if(sel == 2'b1) begin
        sout = p1s ;
    end
    else begin
        sout = p0 ;
    end
end
```